load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")
load("@bazel_skylib//lib:selects.bzl", "selects")

# buildifier: keep sorted
_TESTS = [
    "container",
    "core",
    "entity",
    "version",
]

_COPTS = selects.with_or({
    ("//conditions:default", "@rules_cc//cc/compiler:clang", "@rules_cc//cc/compiler:gcc", "@rules_cc//cc/compiler:mingw-gcc"): [
        "-std=c++17",
        "-w",
    ],
    ("@rules_cc//cc/compiler:msvc-cl", "@rules_cc//cc/compiler:clang-cl"): [
        "/std:c++17",
        "/permissive-",
        "/w",
    ],
})

cc_library(
    name = "common",
    copts = _COPTS,
    hdrs = glob(["entt/common/**/*.h", "entt/common/**/*.hpp"]),
)

[cc_test(
    name = test,
    copts = _COPTS,
    srcs = glob([
        "entt/{}/**/*.cpp".format(test),
        "entt/{}/**/*.hpp".format(test),
        "entt/{}/**/*.h".format(test),
    ]),
    deps = [
        ":common",
        "@entt",
        "@googletest//:gtest",
        "@googletest//:gtest_main",
    ],
) for test in _TESTS]
